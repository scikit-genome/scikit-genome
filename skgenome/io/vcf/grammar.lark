%import common.DIGIT
%import common.LETTER
%import common.NEWLINE

EOL                         : NEWLINE
FLOAT                       : /[-+]?\d*\.?\d+([eE][-+]?\d+)?|[-+]?(INF|INFINITY|NAN)/i
IDENTIFIER                  : /[a-zA-Z_]\w*/
INTEGER                     : /0|[1-9]\d*/i
STRING                      : /[ubf]?r?("(?!"").*?(?<!\\)(\\\\)*?"|'(?!'').*?(?<!\\)(\\\\)*?')/i
TAB                         : /\t/

source                      : records
records                     : record+
record                      : "##" record_kind EOL
record_kind                 : file_format_record
                            | file_date_record
                            | filter_record
                            | format_record
                            | source_record
                            | reference_record
                            | information_record
                            | contig_record
                            | phasing_record
file_date_record            : "fileDate" "=" INTEGER
file_format_record          : "fileformat" "=" "VCFv4.3"
contig_record               : "contig" "=" "<" contig_record_fields ">"
contig_record_fields        : contig_record_field ("," contig_record_field)+
contig_record_field         : assembly_field
                            | identifier_field
                            | length_field
                            | md5_field
                            | species_field
                            | taxonomy_field
                            | url_field
filter_record               : "FILTER" "=" "<" filter_record_fields ">"
filter_record_fields        : filter_record_field ("," filter_record_field)+
filter_record_field         : description_field
                            | identifier_field
format_record               : "FORMAT" "=" "<" format_record_fields ">"
format_record_fields        : format_record_field ("," format_record_field)+
format_record_field         : description_field
                            | identifier_field
                            | number_field
                            | type_field
information_record          : "INFO" "=" "<" information_record_fields ">"
information_record_fields   : information_record_field ("," information_record_field)+
information_record_field    : description_field
                            | identifier_field
                            | number_field
                            | type_field
                            | source_field
                            | version_field
phasing_record              : "phasing" "=" "partial"
reference_record            : "reference" "=" "file:///seq/references/1000GenomesPilot-NCBI36.fasta"
source_record               : "source" "=" "myImputationProgramV3.1"
assembly_field              : "assembly" "=" field_kind
description_field           : "Description" "=" field_kind
identifier_field            : "ID" "=" field_kind
length_field                : "length" "=" field_kind
md5_field                   : "md5" "=" field_kind
number_field                : "Number" "=" field_kind
source_field                : "Source" "=" field_kind
species_field               : "species" "=" field_kind
taxonomy_field              : "taxonomy" "=" field_kind
type_field                  : "Type" "=" type_kind
type_kind                   : "Character"
                            | "Flag"
                            | "Float"
                            | "Integer"
                            | "String"
url_field                   : "url" "=" field_kind
version_field               : "Version" "=" field_kind
field_kind                  : FLOAT
                            | IDENTIFIER
                            | INTEGER
                            | STRING
